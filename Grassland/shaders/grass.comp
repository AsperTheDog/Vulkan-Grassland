#version 450
layout(local_size_x = 256) in;

layout(binding = 0) uniform sampler2D heightmap;
layout(binding = 1) uniform sampler2D grassHeightNoise;

struct GrassInstance {
    vec3 position;
    float rotation;
    vec2 uv;
    float height;
};

layout(binding = 2) buffer GrassBuffer {
    GrassInstance grassPositions[];
};

layout(push_constant) uniform PushConstants {
    vec2 centerPos;
    vec2 worldOffset;
    uvec4 tileGridSizes;
    uvec4 tileDensities;
    float tileSize;
    float gridExtent;
    float heightmapScale;
    float grassBaseHeight;
    float grassHeightVariation;
} pushConstants;

struct TileData {
    vec2 offset;
    uint density;
    uint bladeOffset;
};

float random(float seed)
{
    seed = fract(seed * 0.1031);
    seed *= seed + 33.33;
    seed *= seed + seed;
    return fract(seed);
}

uint getGlobalPosIndex(uint innerRing, uint outerRing, uint gridSize, uint localIndex)
{
    uint midSize = outerRing - innerRing;
    uint midHalfSize = midSize / 2;
    uint fullRows = min(localIndex / outerRing, midHalfSize);
    uint currentIndex = uint(max(0, int(localIndex) - int(midHalfSize * outerRing)));
    uint innerRowOffset = currentIndex % midSize;
    uint midRows = min(currentIndex / midSize, innerRing);
    currentIndex = max(0, int(currentIndex) - int(innerRing * midSize));
    fullRows += currentIndex / outerRing;
    bool extraRow = fullRows == midHalfSize && innerRowOffset >= midHalfSize && midRows < innerRing;

    uint externSize = gridSize - outerRing;
    uint externHalfSize = externSize / 2;
    uint externOffset = (gridSize * externHalfSize) + (fullRows + midRows) * externSize + externHalfSize;
    uint innerOffset = (midRows + int(extraRow)) * innerRing;
    return localIndex + externOffset + innerOffset;
}

TileData getTileData()
{
    uint globalIndex = gl_GlobalInvocationID.x;

    uint tileCount1 = pushConstants.tileGridSizes.x * pushConstants.tileGridSizes.x;
    uint tileCount2 = pushConstants.tileGridSizes.y * pushConstants.tileGridSizes.y - tileCount1;
    uint tileCount3 = pushConstants.tileGridSizes.z * pushConstants.tileGridSizes.z - tileCount1 - tileCount2;
    uint tileCount4 = pushConstants.tileGridSizes.w * pushConstants.tileGridSizes.w - tileCount1 - tileCount2 - tileCount3;

    uint instanceCount1 = pushConstants.tileDensities.x * pushConstants.tileDensities.x * tileCount1;
    uint instanceCount2 = pushConstants.tileDensities.y * pushConstants.tileDensities.y * tileCount2;
    uint instanceCount3 = pushConstants.tileDensities.z * pushConstants.tileDensities.z * tileCount3;
    uint instanceCount4 = pushConstants.tileDensities.w * pushConstants.tileDensities.w * tileCount4;
    
    uint offsets[4] = {0, instanceCount1, instanceCount1 + instanceCount2, instanceCount1 + instanceCount2 + instanceCount3};
    uint innerRings[4] = {0, pushConstants.tileGridSizes.x, pushConstants.tileGridSizes.y, pushConstants.tileGridSizes.z};
    uint outerRings[4] = {pushConstants.tileGridSizes.x, pushConstants.tileGridSizes.y, pushConstants.tileGridSizes.z, pushConstants.tileGridSizes.w};
    uint densities[4] = {pushConstants.tileDensities.x, pushConstants.tileDensities.y, pushConstants.tileDensities.z, pushConstants.tileDensities.w};

    uint ringIndex = int(globalIndex >= offsets[1]) + int(globalIndex >= offsets[2]) + int(globalIndex >= offsets[3]);
    uint density = densities[ringIndex] * densities[ringIndex];
    uint localIndex = globalIndex - offsets[ringIndex];
    uint localTileIndex = localIndex / density;

    uint globalPosIndex = getGlobalPosIndex(innerRings[ringIndex], outerRings[ringIndex], pushConstants.tileGridSizes.w, localTileIndex);

    TileData data;
    data.offset = vec2(globalPosIndex % pushConstants.tileGridSizes.w, globalPosIndex / pushConstants.tileGridSizes.w) * pushConstants.tileSize;
    data.offset -= vec2(pushConstants.tileGridSizes.w * pushConstants.tileSize / 2.0);
    data.density = densities[ringIndex];
    data.bladeOffset = localIndex % density;
    return data;
}

void main()
{
    uint globalIndex = gl_GlobalInvocationID.x;
    if (globalIndex >= grassPositions.length())
        return;
    
    TileData tileData = getTileData();

    uint bladesPerTile = tileData.density * tileData.density;

    ivec2 tileCoord = ivec2(tileData.bladeOffset % tileData.density, tileData.bladeOffset / tileData.density);
    
    float grassAreaSize = pushConstants.tileSize / float(tileData.density);
    vec2 globalPos = pushConstants.centerPos + tileData.offset + vec2(tileCoord) * grassAreaSize;

    vec3 pos = vec3(globalPos.x, 0.0, globalPos.y);
    pos.x += random(pos.z * 2.3411) * grassAreaSize;
    pos.z += random(pos.x * 5.2334) * grassAreaSize;
    vec2 heightmapUV = (pos.xz - pushConstants.worldOffset) / pushConstants.gridExtent;
    pos.y = -texture(heightmap, heightmapUV).r * pushConstants.heightmapScale;

    float height = texture(grassHeightNoise, heightmapUV).r * pushConstants.grassHeightVariation + pushConstants.grassBaseHeight;

    grassPositions[globalIndex].position = pos;
    grassPositions[globalIndex].rotation = random(pos.x + pos.z) * 2.0 * 3.14159265359;
    grassPositions[globalIndex].height = height;
    grassPositions[globalIndex].uv = heightmapUV;
}